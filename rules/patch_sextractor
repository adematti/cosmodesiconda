diff -urN sextractor-2.19.5_orig/acx_lapacke.m4 sextractor-2.19.5/acx_lapacke.m4
--- sextractor-2.19.5_orig/acx_lapacke.m4	1969-12-31 16:00:00.000000000 -0800
+++ sextractor-2.19.5/acx_lapacke.m4	2019-03-11 09:35:21.361155271 -0700
@@ -0,0 +1,79 @@
+dnl
+dnl				acx_lapacke.m4
+dnl
+dnl Set up options for using the lapacke library.
+dnl
+dnl This file is part of a patch to the astromatic toolkit,
+dnl to allow use of fftw and separate lapacke, rather than MKL.
+dnl
+
+AC_DEFUN([ACX_LAPACKE], [
+acx_lapacke_ok=no
+
+LAPACK_H=""
+LAPACKE_H=""
+LAPACKE_LIBS=""
+
+AC_ARG_WITH([lapacke], [AC_HELP_STRING([--with-lapacke=<link command>], [use specified LAPACKE link command.  Set to "no" to disable.])])
+
+if test x"$with_lapacke" != x; then
+   if test x"$with_lapacke" != xno; then
+      LAPACKE_LIBS="$with_lapacke"
+   else
+      acx_lapacke_ok=disable
+   fi
+fi
+
+if test $acx_lapacke_ok = disable; then
+   echo "**** LAPACKE explicitly disabled by configure."
+else
+
+    # check for both standard and MKL headers
+    acx_lapacke_header_ok=no
+    AC_CHECK_HEADER([mkl_lapacke.h], [acx_lapacke_header_ok=yes], [])
+    if test $acx_lapacke_header_ok = yes; then
+       AC_DEFINE_UNQUOTED(LAPACK_H, "mkl_lapack.h", [LAPACK header filename.])
+       AC_DEFINE_UNQUOTED(LAPACKE_H, "mkl_lapacke.h", [LAPACKe header filename.])
+    else
+       AC_CHECK_HEADER([lapacke.h], [acx_lapacke_header_ok=yes], [])
+       if test $acx_lapacke_header_ok = yes; then
+       	  AC_DEFINE_UNQUOTED(LAPACK_H, "lapacke.h", [LAPACK header filename.])
+	  AC_DEFINE_UNQUOTED(LAPACKE_H, "lapacke.h", [LAPACKe header filename.])
+       else
+          AC_MSG_ERROR([Cannot find lapacke header])
+       fi
+    fi
+
+    # First, check LAPACKE_LIBS variable
+    if test "x$LAPACKE_LIBS" != x; then
+        save_LIBS="$LIBS"; LIBS="$LAPACKE_LIBS $LIBS"
+        AC_MSG_CHECKING([for LAPACKE_cheev in $LAPACKE_LIBS])
+        AC_TRY_LINK_FUNC(LAPACKE_cheev, [acx_lapacke_ok=yes], [LAPACKE_LIBS=""])
+        AC_MSG_RESULT($acx_lapacke_ok)
+        LIBS="$save_LIBS"
+        if test $acx_lapacke_ok = no; then
+            LAPACKE_LIBS=""
+        fi
+    fi
+
+    # Generic LAPACKE library?
+    if test $acx_lapacke_ok = no; then
+        save_LIBS="$LIBS"; LIBS="-llapacke $LIBS"
+        AC_CHECK_LIB(lapacke, LAPACKE_cheev,
+            [acx_lapacke_ok=yes; LAPACKE_LIBS="-llapacke"], [], [])
+        LIBS="$save_LIBS"
+    fi
+
+fi
+
+AC_SUBST(LAPACKE_LIBS)
+
+if test x"$acx_lapacke_ok" != xyes; then
+  AC_MSG_ERROR([cannot link to lapacke!])
+fi
+
+AC_DEFINE(HAVE_LAPACK,1, [Define if you have the LAPACK libraries.])
+AC_DEFINE(HAVE_LAPACKE,1, [Define if you have the LAPACKe libraries.])
+
+])dnl ACX_LAPACKE
+
diff -urN sextractor-2.19.5_orig/configure.ac sextractor-2.19.5/configure.ac
--- sextractor-2.19.5_orig/configure.ac	2013-12-14 09:12:32.000000000 -0800
+++ sextractor-2.19.5/configure.ac	2019-03-11 09:35:21.365155282 -0700
@@ -45,7 +45,7 @@
 AC_SUBST(DATE3, "$date3")
 
 # Include macros
-sinclude(acx_atlas.m4)
+sinclude(acx_lapacke.m4)
 sinclude(acx_fftw.m4)
 sinclude(acx_mkl.m4)
 sinclude(acx_prog_cc_optim.m4)
@@ -96,6 +96,7 @@
 fi
 LT_INIT
 AC_PROG_INSTALL
+AC_PROG_RANLIB
 
 # Checks for libraries.
 AC_CHECK_LIB(m, sin)
@@ -131,14 +132,6 @@
 AC_SYS_LARGEFILE
 AC_FUNC_FSEEKO
 
-# Provide special options for ATLAS
-AC_ARG_WITH(atlas-libdir,
-        [AS_HELP_STRING([--with-atlas-libdir=<ATLAS library path>],
-        [Provide an alternative path to the ATLAS library])])
-AC_ARG_WITH(atlas-incdir,
-        [AS_HELP_STRING([--with-atlas-incdir=<ATLAS include dir>],
-        [Provide an alternative path to the ATLAS include directory])])
-
 # Provide special options for FFTW
 AC_ARG_WITH(fftw-libdir,
         [AS_HELP_STRING([--with-fftw-libdir=<FFTW library path>],
@@ -238,19 +231,15 @@
   else
 ################ handle the FFTW library (Fourier transforms) ################
     ACX_FFTW($with_fftw_libdir,$with_fftw_incdir,$use_pthreads,yes,
-	LIBS="$FFTW_LIBS $LIBS"
-	if test x$FFTW_WARN != x; then
-	  AC_MSG_WARN([$FFTW_WARN])
-	fi,
-	AC_MSG_ERROR([$FFTW_ERROR Exiting.]))
-################## handle the ATLAS library(linear algebra) ##################
-    ACX_ATLAS($with_atlas_libdir,$with_atlas_incdir,$use_pthreads,
-	LIBS="$ATLAS_LIBS $LIBS"
-	if test x$ATLAS_WARN != x; then
-	  AC_MSG_WARN([$ATLAS_WARN])
-	fi,
-	AC_MSG_ERROR([$ATLAS_ERROR Exiting.]))
-  fi
+    LIBS="$FFTW_LIBS $LIBS"
+    if test x$FFTW_WARN != x; then
+      AC_MSG_WARN([$FFTW_WARN])
+    fi,
+    AC_MSG_ERROR([$FFTW_ERROR Exiting.]))
+######## look for standalone lapacke library ###########
+    ACX_LAPACKE
+    LIBS="$LAPACKE_LIBS $LIBS"
+  fi  
 fi
 AM_CONDITIONAL(USE_MODEL, [test x$enable_model_fitting != xno])
 
